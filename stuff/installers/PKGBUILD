# Maintainer: Giorgio Wicklein <giowckln@gmail.com>
pkgname=symphytum
pkgver=1.1
pkgrel=1
pkgdesc="Personal database software"
arch=('i686' 'x86_64')
url="http://giowck.github.io/symphytum/"
license=('BSD')
depends=('qt4' 'sqlite')
optdepends=('python2: dropbox sync support' 'python2-setuptools: dropbox sync support')
source=(http://giowck.github.io/symphytum/files/$pkgname-$pkgver-src.tar.gz)
md5sums=('1f88c56ee50fea6ea4a0b3c7fb3aad16')

build() {
  cd "$srcdir/$pkgname-$pkgver-src"

  qmake-qt4 -config release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src"

  # Binary
  install -Dm755 "$srcdir/$pkgname-$pkgver-src/symphytum" "${pkgdir}/usr/bin/symphytum"

  # Icons and desktop files
  install -d "${pkgdir}/usr/share/"
  cp -R "$srcdir/$pkgname-$pkgver-src/stuff/installers/deb/usr/share/applications/" "${pkgdir}/usr/share/"
  cp -R "$srcdir/$pkgname-$pkgver-src/stuff/installers/deb/usr/share/pixmaps/" "${pkgdir}/usr/share/"

  # Sync framework files
  install -d "${pkgdir}/usr/share/symphytum/"
  cp -R "$srcdir/$pkgname-$pkgver-src/stuff/installers/deb/usr/share/symphytum/" "${pkgdir}/usr/share/"

  # Copy license
  install -Dm644 "$srcdir/$pkgname-$pkgver-src/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
